options:
	p: &7[&eArtifacts&7] &e
	e: &c[Artifacts] &c

command /solarblade:
	executable by: players
	trigger:
		set {_n} to player
		if {_n} isn't "Sarcasticphoenix":
			message "{@p}You fail to summon the blade from thin air."
			stop
		set tool of player to gold sword named "&6Solar Blade"
		enchant tool of player with unbreaking 10000
		enchant tool of player with sharpness 10
		enchant tool of player with knockback 3
		message "{@p}The blade has been summoned."
		spawn 50 of particle fireworks spark offset by 2, 2, 2 at location of player
		spawn 50 of particle fireworks spark:1 offset by 2, 2, 2 at location of player
		wait 1 second
		set {_z} to 0
		while {_z} is less than 25:
			spawn 10 of particle flame:1 offset by 5, 5, 5 at location of player
			spawn 10 of particle enchantment table:1 offset by 5, 5, 5 at location of player
			wait 0.2 seconds
			add 1 to {_z}
		stop

on right click holding gold sword:
	name of tool of player is "&6Solar Blade"
	set {_n} to player
	if {_n} isn't "Sarcasticphoenix":
		set health of player to 1
		spawn 500 of particle lava offset by 5, 5, 5 at location of player
		set tool of player to air
		stop
	set {_p} to 10
	loop 25 times:
		spawn {_p} of particle flame:1 offset by 3, 3, 3 at location of player
		spawn {_p} of particle enchantment table:1 offset by 3, 3, 3 at location of player
		add 1 to {_p}
		wait 0.2 seconds
	strike lightning effect at location of player
	spawn 2 of particle huge explosion offset by 0, 0, 0 at location of player
	spawn 300 of particle fireworks spark:1 offset by 0.5, 0.5, 0.5 at location of player
	spawn 300 of particle lava offset by 5, 5, 5 at location of player
	loop entities in radius 10 of location of player:
		if loop-entity isn't player:
			damage the loop-entity by 1 heart
			chance of 50%:
				ignite the loop-entity
			push the loop-entity upwards at speed 1.5
			push the loop-entity backwards at speed 1.5
	stop

on mine of stone:
	world is "survival"
	gamemode of player is survival
	if y-coordinate of block is less than 10:
		set {_z} to 0.00015
	else:
		set {_z} to 0.0001
	chance of {_z}:
		message "{@p}Stone crumbles, exposing an ancient item."
		cancel event
		set block to stained glass
		spawn 25 of particle fireworks spark offset by 2, 2, 2 at location of block
		set {_z} to location of block
		chance of 30%:
			drop 25 diamonds at {_z}
			stop
		chance of 95%:
			set {_z} to rounded random number between 1 and 13
			if {_z} is 1:
				give player an iron sword of sharpness 3 and knockback 1 and unbreaking 10 named "&bFrostbane"
			else if {_z} is 2:
				give player an iron sword of sharpness 3 and unbreaking 10 named "&bSting"
			else if {_z} is 3:
				give player a stone sword of knockback 3 and unbreaking 10 and sharpness 2 named "&bBarbarian Club"
			else if {_z} is 4:
				give player an iron axe of knockback 2 and unbreaking 10 and sharpness 3 named "&bAres Battleaxe"
			else if {_z} is 5:
				give player a gold sword of sharpness 3 and unbreaking 10 and knockback 4 named "&aWitherbane"
			else if {_z} is 6:
				give player a diamond sword of sharpness 3 and unbreaking 10 and smite 5 named "&aZombieslayer"
			else if {_z} is 7:
				give player a bow of power 3 and unbreaking 10 and punch 2 named "&bArtemis Bow"
			else if {_z} is 8:
				give player a bow of power 2 and unbreaking 10 and flame 3 named "&bFury"
			else if {_z} is 9:
				give player a bow of power 3 and flame 2 and punch 1 and unbreaking 10 named "&aRage"
			else if {_z} is 10:
				give player an iron helmet of protection 2 and unbreaking 10 and thorns 1 named "&bPolished helm"
			else if {_z} is 11:
				give player a chain helmet of thorns 4 and unbreaking 10 named "&bBacklash"
			else if {_z} is 12:
				give player a wither skeleton head of protection 4 and aqua affinity 1 and unbreaking 10 named "&aPhantom"
			else if {_z} is 13:
				give player a chain chestplate of protection 2 and unbreaking 10 and thorns 1 named "&bBarrier"
		stop

on tool change:
	wait 1 tick
	name of tool of player contains "&a" or "&b" or "&6"
	world is "survival" or "SuperflatSurvival"
	gamemode of player is survival
	if player doesn't contain a water bottle named "&1Potion of Handle Artifact":
		if name of tool of player contains "&a":
			damage player by 3 hearts
		else if name of tool of player contains "&b":
			damage player by 4 hearts
		else if name of tool of player contains "&6":
			damage player by 5 hearts
		message "{@p}A magical force from your artifact blasts you!"
		spawn 100 of particle lava offset by 1, 1, 1 at location of player
		apply blindness to the player for 3 seconds
	else:
		message "{@p}Having a potion of handle artifact, the artifact settles into your hands."
		spawn 25 of particle explode:1 offset by 1, 1, 1 at location of player
	stop

on right click holding ghast tear:
	name of tool of player is "&aAmulet of Levitation"
	cancel event
	if {Lev.%uuid of player%} is not true:
		set fly mode of player to true
		set {Lev.%uuid of player%} to true
		message "{@p}The amulet glows for a moment, then fades."
		message "{@p}You feel lighter."
		while player is online:
			if player doesn't contain 1 ghast tear named "&aAmulet of Levitation":
				message "{@p}You suddenly feel much heavier."
				set fly mode of player to false
				set {Lev.%uuid of player%} to false
				wait 1 second
				stop
			else:
				if fly mode of player is false:
					stop
				if player contains 1 cobble:
					remove 1 cobble from the player
				else:
					message "{@p}Your amulet has no more cobble to use as fuel."
					message "{@p}You suddenly feel much heavier."
					set {Lev.%uuid of player%} to false
					set fly mode of player to false
					stop
				wait 5 seconds
		stop
	else:
		set {Lev.%uuid of player%} to false
		set fly mode of player to false
		message "{@p}Your amulet glows, then fades."
		message "{@p}You feel much heavier."
		stop

on join:
	if {Lev.%uuid of player%} is true:
		set fly mode of player to false
		set {Lev.%uuid of player%} to false
	stop

