options:
	p: &7[&cPassword&7] &c

command /password <text> [<text>]:
	permission: quiz.bypass
	trigger:
		if arg 1 is "set":
			if {Password.%player%} is set:
				message "{@p}Password is already set. Clear it before setting it to something different"
				stop
			if {Password.%player%.loggedin} is false:
				message "{@p}Log in first."
				stop
			if arg 2 is not set:
				message "{@p}Password required"
				stop
			set {Password.%player%} to hashed arg 2
			message "{@p}Set to %arg 2%"
			stop
		else if arg 1 is "clear":
			if arg 2 is set:
				player has permission "adminchat.adminchat"
				delete {Password.%arg 2%}
				message "{@p}%arg 2%'s password cleared"
				stop
			if {Password.%player%} is not set:
				message "{@p}Password not set"
				stop
			if {Password.%player%.loggedin} is false:
				message "{@p}Log in first."
				stop
			delete {Password.%player%}
			message "{@p}Cleared"
			stop
		else if arg 1 is "setlobby":
			player has permission "adminchat.adminchat"
			set {Password.lobby} to location of player
			message "{@p}set"
		else:
			if {Password.%player%} is hashed arg 1:
				message "{@p}Logged in!"
				set {Password.%player%.loggedin} to true
				teleport player to {Password.temp.%player%.loc}
				delete {Password.temp.%player%.loc}
				stop
			else:
				message "{@p}Wrong password."
				add 1 to {Password.temp.%player%.attempts}
				if {Password.temp.%player%.attempts} is greater than or equal to 5:
					kick the player due to "Too many failed attempts"
					delete {Password.temp.%player%.attempts}
				stop

on join:
	{Password.%player%} is set
	if {Password.temp.%player%.loc} is not set:
		set {Password.temp.%player%.loc} to location of player
	set {Password.%player%.loggedin} to true
	teleport player to {Password.lobby}
	set {Password.%player%.loggedin} to false
	wait 0.5 seconds
	message "{@p}Welcome. Please log in with your password."
	message "{@p}/password YourPasswordHere"
	stop

on quit:
	{Password.%player%} is set
	set {Password.%player%.loggedin} to false
	stop

on command:
	{Password.%player%} is set
	{Password.%player%.loggedin} is false
	command isn't "password"
	cancel event
	message "{@p}Please log in first. /password YourPassword"
	stop

on teleport:
	{Password.%player%} is set
	{Password.%player%.loggedin} is false
	cancel event
	message "{@p}Please log in first. /password YourPassword"
	stop

